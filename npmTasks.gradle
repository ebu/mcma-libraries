import groovy.json.JsonOutput
import groovy.json.JsonSlurper

task setVersion {
    def packageJson = file("package.json")

    inputs.file(packageJson)
    inputs.property("VERSION", VERSION)
    outputs.file(packageJson)

    doLast {
        def jsonSlurper = new JsonSlurper()
        def object = jsonSlurper.parse(file("package.json"))

        object.version = VERSION

        if (object.dependencies != null) {
            object.dependencies.keySet().each {
                object.dependencies[it] = it.startsWith("@mcma/") ? VERSION : object.dependencies[it]
            }
        }
        if (object.peerDependencies != null) {
            object.peerDependencies.keySet().each {
                object.peerDependencies[it] = it.startsWith("@mcma/") ? VERSION : object.peerDependencies[it]
            }
        }
        if (object.devDependencies != null) {
            object.devDependencies.keySet().each {
                object.devDependencies[it] = it.startsWith("@mcma/") ? VERSION : object.devDependencies[it]
            }
        }

        def json = JsonOutput.toJson(object)
        def jsonPretty = JsonOutput.prettyPrint(json) + "\n"

        packageJson.write(jsonPretty)
    }
}

task npmInstall(type: Exec) {
    dependsOn ":verifyNodeJS"
    inputs.file("package.json")
    outputs.file("package-lock.json")
    outputs.dir("node_modules")
    commandLine npmExecutable
    args "install"
}

task npmUpdate(type: Exec) {
    dependsOn ":verifyNodeJS"
    inputs.file "package.json"
    inputs.property("todaysDate", new Date().clearTime())
    outputs.upToDateWhen { true }
    commandLine npmExecutable
    args "update"
}

task tsc(type: Exec) {
    dependsOn ":verifyTsc"
    dependsOn npmInstall
    inputs.file("tsconfig.json")
    inputs.file("package.json")
    inputs.dir("lib")
    inputs.dir("node_modules")
    outputs.dir("dist")

    commandLine tscExecutable

    doFirst {
        delete "dist"
    }
}

task npmPublish(type: Exec) {
    dependsOn ":verifyNodeJS"
    dependsOn setVersion
    dependsOn tsc
    inputs.file("package.json")
    commandLine npmExecutable
    args "publish", "--access", "public", "--registry", "https://registry.npmjs.org"

    def jsonSlurper = new JsonSlurper()
    def packageJson = jsonSlurper.parse(file("package.json"))
    String packageName = packageJson.name + "@" + VERSION

    onlyIf {
        // check if package is already published
        def stdout = new ByteArrayOutputStream()
        def stderr = new ByteArrayOutputStream()
        try {
            exec {
                commandLine npmExecutable
                args "view", packageName, "--registry", "https://registry.npmjs.org"
                standardOutput stdout
                errorOutput stderr
            }
        } catch (ignored) {
        }

        return stdout.toString().trim().isEmpty()
    }

    doLast {
        def tempDir = File.createTempDir()
        boolean available = false
        while (!available) {
            try {
                sleep(15000)
                exec {
                    commandLine npmExecutable
                    args "install", packageName, "--no-save"
                    workingDir tempDir
                }
                available = true
                println "Package " + packageName + " available"
            } catch (ignored) {
                println "Package " + packageName + " not yet available"
            }
        }
        delete tempDir
    }
}

task npmPublishLocal(type: Exec) {
    dependsOn ":verifyNodeJS"
    dependsOn setVersion
    dependsOn tsc
    inputs.file("package.json")
    commandLine npmExecutable
    args "publish", "--access", "public", "--registry", "http://localhost:4873"

    def jsonSlurper = new JsonSlurper()
    def packageJson = jsonSlurper.parse(file("package.json"))
    String packageName = packageJson.name + "@" + VERSION

    onlyIf {
        // check if package is already published
        def stdout = new ByteArrayOutputStream()
        def stderr = new ByteArrayOutputStream()
        try {
            exec {
                commandLine npmExecutable
                args "view", packageName, "--registry", "http://localhost:4873"
                standardOutput stdout
                errorOutput stderr
            }
        } catch (ignored) {
        }

        return stdout.toString().trim().isEmpty()
    }
}

task clean(type: Delete) {
    delete "dist"
    delete "node_modules"
}

task cleanPackageLocks(type: Delete) {
    delete "package-lock.json"
}

task npmAuditFix(type: Exec) {
    dependsOn ":verifyNodeJS"
    inputs.file("package.json")
    commandLine npmExecutable
    args "audit", "fix"
}