import groovy.json.JsonOutput
import groovy.json.JsonSlurper

def requiredVersionNodeJS = "10.16.3"

allprojects {
    ext {
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            nodeExecutable = Arrays.asList("node")
            npmExecutable = Arrays.asList("cmd", "/c", "npm")
        } else {
            nodeExecutable = Arrays.asList("node")
            npmExecutable = Arrays.asList("npm")
        }
    }
}

def getVersionNodeJS = { ->
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    try {
        exec {
            commandLine nodeExecutable
            args "--version"
            standardOutput stdout
            errorOutput stderr
        }
    } catch (Exception ignored) {
    }

    return stdout.toString().trim().replace("v", "")
}

task verifyNodeJS {
    inputs.property("VERSION_NODE_JS", requiredVersionNodeJS)
    outputs.upToDateWhen { getVersionNodeJS() == requiredVersionNodeJS }
    doLast {
        def versionNodeJs = getVersionNodeJS()
        if (versionNodeJs != requiredVersionNodeJS) {
            throw new Exception("Required Node version " + requiredVersionNodeJS + " not installed. Detected version " + versionNodeJs)
        }
    }
}

task build {
    dependsOn "aws:build"
    dependsOn "base:build"
}

task clean {}

subprojects {
    task setVersion {
        def packageJson = file("package.json")

        enabled packageJson.exists()
        inputs.file(packageJson)
        inputs.property("VERSION", VERSION)
        outputs.file(packageJson)

        doLast {
            def jsonSlurper = new JsonSlurper()
            def object = jsonSlurper.parse(file("package.json"))

            object.version = VERSION

            if (object.peerDependencies != null) {
                object.peerDependencies.keySet().each {
                    object.peerDependencies[it] = it.startsWith("@mcma/") ? VERSION : object.peerDependencies[it]
                }
            }
            if (object.devDependencies != null) {
                object.devDependencies.keySet().each {
                    object.devDependencies[it] = it.startsWith("@mcma/") ? VERSION : object.devDependencies[it]
                }
            }

            def json = JsonOutput.toJson(object)
            def jsonPretty = JsonOutput.prettyPrint(json) + "\n"

            packageJson.write(jsonPretty)
        }
    }

    task npmInstall(type: Exec) {
        dependsOn verifyNodeJS
        enabled file("package.json").exists()
        inputs.file("package.json")
        outputs.file("package-lock.json")
        outputs.dir("node_modules")
        commandLine npmExecutable
        args "install"
    }

    task npmUpdate(type: Exec) {
        dependsOn verifyNodeJS
        enabled file("package.json").exists()
        inputs.file "package.json"
        inputs.property("todaysDate", new Date().clearTime())
        outputs.upToDateWhen { true }
        commandLine npmExecutable
        args "update"
    }

    task npmPublish(type: Exec) {
        dependsOn verifyNodeJS
        enabled file("package.json").exists()
        inputs.file("package.json")
        commandLine npmExecutable
        args "publish", "--access", "public", "--registry", "https://registry.npmjs.org"
    }

    task npmPublishLocal(type: Exec) {
        dependsOn verifyNodeJS
        enabled file("package.json").exists()
        inputs.file("package.json")
        commandLine npmExecutable
        args "publish", "--access", "public", "--registry", "http://localhost:4873"
    }

    task clean(type: Delete) {
        delete "build"
        delete "node_modules"
    }
}

afterEvaluate {
    // avoiding simultaneous connections to npmjs.com
    def prevTask = null
    project.subprojects.each {
        def task = it.tasks.find { task -> task.name.contains('npmUpdate') }
        if (task != null) {
            if (prevTask != null) {
                task.mustRunAfter(prevTask)
            }
            prevTask = task
        }
    }

    prevTask = null
    project.subprojects.each {
        def task = it.tasks.find { task -> task.name.contains('npmInstall') }
        if (task != null) {
            if (prevTask != null) {
                task.mustRunAfter(prevTask)
            }
            prevTask = task
        }
    }
}
